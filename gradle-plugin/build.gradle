plugins {
  id 'groovy'
  id 'java-gradle-plugin'
  id 'maven-publish'
  id 'com.palantir.git-version' version '0.10.1'
  id 'com.gradle.plugin-publish' version '0.11.0'
}

group = "com.lightbend.akka.grpc"
// https://github.com/palantir/gradle-git-version/issues/97

def tag = "git describe --tags".execute().text.substring(1).split("-g")[0].replace("\n", "")
def finalVersion = (tag == versionDetails().lastTag.substring(1)) ? tag : tag.reverse().reverse() + "-" + versionDetails().gitHash.substring(0, 8)

version = finalVersion

gradlePlugin {
  plugins {
    pekkoGrpcPlugin {
      id = 'org.apache.pekko.grpc.gradle'
      implementationClass = 'org.apache.pekko.grpc.gradle.PekkoGrpcPlugin'
    }
  }
}

pluginBundle {
  website = 'https://doc.akka.io/docs/akka-grpc/current/'
  vcsUrl = 'https://github.com/apache/incubator-pekko-grpc'
  description = 'Building streaming gRPC servers and clients on top of Apache Pekko Streams'
  tags = ['pekko', 'streams', 'reactive']

  plugins {
    pekkoGrpcPlugin {
      id = 'org.apache.pekko.grpc.gradle'
      displayName = 'Apache Pekko gRPC'
    }
  }
}

jar {
  manifest {
    attributes 'Implementation-Version': project.version
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'com.google.protobuf:protobuf-gradle-plugin:0.8.19'
  implementation 'commons-lang:commons-lang:2.6'
  testImplementation('org.spockframework:spock-core:1.3-groovy-2.5')
}

test {
  systemProperty 'pekkoGrpcTest.pluginVersion', project.version
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat "full"
    showStandardStreams = false
  }
}
